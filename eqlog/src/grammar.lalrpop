use crate::ast::*;
use crate::signature::Signature;
use std::iter::once;
use crate::analysis::check_semantically;
use crate::unification::TermMap;
use convert_case::{Case, Casing};

grammar;

Id: String = { r"[A-Za-z][A-Za-z0-9'_]*" => <>.to_string(), }

SnakeCaseId: String = {
    <id: Id> => { 
        assert_eq!(id.to_case(Case::Snake), id, "Expected identifier in snake_case");
        id
    },
}

CamelCaseId: String = {
    <id: Id> => { 
        assert_eq!(id.to_case(Case::UpperCamel), id, "Expected identifier in CamelCase");
        id
    },
}

SortDecl: Sort = {
    <l: @L>  "Sort" <name: CamelCaseId> ";" <r: @R> => {
        Sort{name, location: Some(Location(l, r))}
    }
}

PredicateDecl: Predicate = {
    <l: @L> "Pred" <name: CamelCaseId> ":" <head: Id> <tail: ("*" <Id>)*> ";" <r: @R> => {
        Predicate {
            name,
            arity: once(head).chain(tail).collect(),
            location: Some(Location(l, r)),
        }
    },
}

FunctionDecl: Function = {
    <l: @L> "Func" <name: CamelCaseId> ":" <head_dom: (<Id>)?> <tail_dom: ("*" <Id>)*> "->" <cod: Id> ";" <r: @R> => {
        Function {
            name,
            dom: head_dom.into_iter().chain(tail_dom).collect(),
            cod,
            location: Some(Location(l, r)),
        }
    },
}

Application: (String, Vec<(Term, TermUniverse)>) = {
    <operator: Id> "(" <head_arg: (<Term>)?> <tail_args: ("," <Term>)*> ")" => {
        let args: Vec<(Term, TermUniverse)> = head_arg.into_iter().chain(tail_args).collect();
        (operator, args)
    }
}

Term: (Term, TermUniverse) = {
    <l: @L> <var: SnakeCaseId> <r: @R> => {
        Term::new_variable(var, Some(Location(l, r)))
    },
    <l: @L> "_" <r: @R> => {
        Term::new_wildcard(Some(Location(l, r)))
    },
    <l: @L> <app: Application> <r: @R> => {
        let (func, args) = app;
        Term::new_application(func, args, Some(Location(l, r)))
    },
}

Atom: (Atom, TermUniverse) = {
    <l: @L> <lhs: Term> "=" <rhs: Term> <r: @R> => {
        Atom::new_equal(lhs, rhs, Some(Location(l, r)))
    },
    <l: @L> <tm: Term> "!" <r: @R> => {
        Atom::new_defined(tm, None, Some(Location(l, r)))
    },
    <l: @L> <tm: Term> "!" ":" <s: Id> <r: @R> => {
        Atom::new_defined(tm, Some(s), Some(Location(l, r)))
    },
    <l: @L> <app: Application> <r: @R> => {
        let (pred, args) = app;
        Atom::new_predicate(pred, args, Some(Location(l, r)))
    },
}

Formula: Vec<(Atom, TermUniverse)> = {
    <head_atom: (<Atom>)?> <tail_atoms : ("&" <Atom>)*> => {
        head_atom.into_iter().chain(tail_atoms).collect()
    }
}

pub Sequent: Sequent = {
    <premise: Formula> "=>" <conclusion: Formula> => Sequent::new_implication(premise, conclusion),
    <from: Term> "~>" <to: Term> => Sequent::new_reduction(Vec::new(), from, to),
    <premise: Formula> "=>" <from: Term> "~>" <to: Term> => Sequent::new_reduction(premise, from, to),
}

AxiomDecl: Axiom = {
    <l: @L> "Axiom" <sequent: Sequent> ";" <r: @R> => Axiom {
        sequent,
        location: Some(Location(l, r)),
    },
}

pub Theory: (Signature, Vec<(Sequent, TermMap<String>)>) = {
    () => (Signature::new(), vec![]),
    <mut theory: Theory> <sort: SortDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_sort(sort);
        theory
    },
    <mut theory: Theory> <pred: PredicateDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_predicate(pred);
        theory
    },
    <mut theory: Theory> <func: FunctionDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_function(func);
        theory
    },
    <mut theory: Theory> <ax: AxiomDecl> => {
        let (ref sig, ref mut axioms) = &mut theory;
        let sorts = check_semantically(&sig, &ax.sequent);
        axioms.push((ax.sequent, sorts));
        theory
    },
}
