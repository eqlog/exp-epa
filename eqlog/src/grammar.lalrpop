use crate::ast::*;
use crate::error::*;
use crate::signature::Signature;
use std::iter::once;
use crate::analysis::check_semantically;
use crate::unification::TermMap;
use convert_case::{Case, Casing};
use std::mem::swap;

grammar(universe: &mut TermUniverse);

extern {
    type Error = CompileError;
}

Id: String = { r"[A-Za-z][A-Za-z0-9'_]*" => <>.to_string(), }

SnakeCaseId: String = {
    <id: Id> => { 
        assert_eq!(id.to_case(Case::Snake), id, "Expected identifier in snake_case");
        id
    },
}

CamelCaseId: String = {
    <id: Id> => { 
        assert_eq!(id.to_case(Case::UpperCamel), id, "Expected identifier in CamelCase");
        id
    },
}

SortDecl: Sort = {
    <l: @L>  "Sort" <name: CamelCaseId> ";" <r: @R> => {
        Sort{name, location: Some(Location(l, r))}
    }
}

PredicateDecl: Predicate = {
    <l: @L> "Pred" <name: CamelCaseId> ":" <head: Id> <tail: ("*" <Id>)*> ";" <r: @R> => {
        Predicate {
            name,
            arity: once(head).chain(tail).collect(),
            location: Some(Location(l, r)),
        }
    },
}

FunctionDecl: Function = {
    <l: @L> "Func" <name: CamelCaseId> ":" <head_dom: (<Id>)?> <tail_dom: ("*" <Id>)*> "->" <cod: Id> ";" <r: @R> => {
        Function {
            name,
            dom: head_dom.into_iter().chain(tail_dom).collect(),
            cod,
            location: Some(Location(l, r)),
        }
    },
}

Application: (String, Vec<Term>) = {
    <operator: Id> "(" <head_arg: (<Term>)?> <tail_args: ("," <Term>)*> ")" => {
        let args: Vec<Term> = head_arg.into_iter().chain(tail_args).collect();
        (operator, args)
    }
}

Term: Term = {
    <l: @L> <var: SnakeCaseId> <r: @R> => {
        universe.new_term(TermData::Variable(var), Some(Location(l, r)))
    },
    <l: @L> "_" <r: @R> => {
        universe.new_term(TermData::Wildcard, Some(Location(l, r)))
    },
    <l: @L> <app: Application> <r: @R> => {
        let (func, args) = app;
        universe.new_term(TermData::Application(func, args), Some(Location(l, r)))
    },
}

Atom: Atom = {
    <l: @L> <lhs: Term> "=" <rhs: Term> <r: @R> => {
        Atom { data: AtomData::Equal(lhs, rhs), location: Some(Location(l, r)) }
    },
    <l: @L> <tm: Term> "!" <r: @R> => {
        Atom { data: AtomData::Defined(tm, None), location: Some(Location(l, r)) }
    },
    <l: @L> <tm: Term> "!" ":" <s: Id> <r: @R> => {
        Atom { data: AtomData::Defined(tm, Some(s)), location: Some(Location(l, r)) }
    },
    <l: @L> <app: Application> <r: @R> => {
        let (pred, args) = app;
        Atom { data: AtomData::Predicate(pred, args), location: Some(Location(l, r)) }
    },
}

Formula: Vec<Atom> = {
    <head_atom: (<Atom>)?> <tail_atoms : ("&" <Atom>)*> => {
        head_atom.into_iter().chain(tail_atoms).collect()
    }
}

pub Sequent: Sequent = {
    <premise: Formula> "=>" <conclusion: Formula> => {
        let mut u = TermUniverse::new();
        swap(&mut u, universe);
        Sequent::new_implication(u, premise, conclusion)
    },
    <from: Term> "~>" <to: Term> => {
        let mut u = TermUniverse::new();
        swap(&mut u, universe);
        Sequent::new_reduction(u, Vec::new(), from, to)
    },
    <premise: Formula> "=>" <from: Term> "~>" <to: Term> => {
        let mut u = TermUniverse::new();
        swap(&mut u, universe);
        Sequent::new_reduction(u, premise, from, to)
    },
}

AxiomDecl: Axiom = {
    <l: @L> "Axiom" <sequent: Sequent> ";" <r: @R> => Axiom {
        sequent,
        location: Some(Location(l, r)),
    },
}

pub Theory: (Signature, Vec<(Axiom, TermMap<String>)>) = {
    () => (Signature::new(), vec![]),
    <mut theory: Theory> <sort: SortDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_sort(sort);
        theory
    },
    <mut theory: Theory> <pred: PredicateDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_predicate(pred);
        theory
    },
    <mut theory: Theory> <func: FunctionDecl> => {
        let (ref mut sig, _) = &mut theory;
        sig.add_function(func);
        theory
    },
    <mut theory: Theory> <ax: AxiomDecl> =>? {
        let (ref sig, ref mut axioms) = &mut theory;
        let sorts = check_semantically(&sig, &ax.sequent)?;
        axioms.push((ax, sorts));
        Ok(theory)
    },
}
