use crate::direct_ast::*;
use crate::indirect_ast;
use crate::signature::Signature;
use std::iter::once;
use crate::analysis::{SequentAnalysis, analyze};

grammar;

Id: String = { r"[A-Za-z][A-Za-z0-9'_]*" => <>.to_string(), }

Sort: Sort = { <name: Id> => Sort(name) }

Predicate: Predicate = {
    <name: Id> ":" <head: Id> <tail: ("*" <Id>)*> => {
        Predicate{name, arity: once(head).chain(tail).collect()}
    },
}

Function: Function = {
    <name: Id> ":" <head_dom: (<Id>)?> <tail_dom: ("*" <Id>)*> "->" <cod: Id> => {
        Function {
            name,
            dom: head_dom.into_iter().chain(tail_dom).collect(),
            cod,
        }
    },
}

Term: Term = {
    <var: Id> => Term::Variable(var),
    "_" => Term::Wildcard,
    <function: Id> "(" <head_arg: (<Term>)?> <tail_args: ("," <Term>)*> ")" => {
        let args: Vec<Term> = head_arg.into_iter().chain(tail_args).collect();
        Term::Application(function, args)
    },
}

Atom: Atom = {
    <lhs: Term> "=" <rhs: Term> => Atom::Equal(lhs, rhs),
    "!" <t: Term> => Atom::Defined(t, None),
    "!" "(" <t: Term> ":" <s: Id> ")" => Atom::Defined(t, Some(s)),
    <pred: Id> "(" <head_arg: (<Term>)?> <tail_args: ("," <Term>)*> ")"
        => Atom::Predicate(pred, head_arg.into_iter().chain(tail_args).collect()),
}

Formula: Formula = {
    <head_atom: (<Atom>)?> <tail_atoms : ("&" <Atom>)*> =>
        Formula(head_atom.into_iter().chain(tail_atoms).collect()),
}

Sequent: Sequent = {
    <premise: Formula> "=>" <conclusion: Formula> => Sequent::SurjectiveImplication(premise, conclusion),
    <premise: Formula> "=!>" <conclusion: Formula> => Sequent::GeneralImplication(premise, conclusion),
    <s: Term> "~>" <t: Term> => Sequent::Reduction(s, t),
    <premise: Formula> "=>" <s: Term> "~>" <t: Term> => Sequent::ConditionalReduction(premise, s, t),
}

Declaration: Declaration = {
    "Sort" <sort: Sort> ";" => Declaration::Sort(sort),
    "Pred" <pred: Predicate> ";" => Declaration::Predicate(pred),
    "Func" <func: Function> ";" => Declaration::Function(func),
    "Axiom" <seq: Sequent> ";" => Declaration::Axiom(seq),
}

pub Theory: (Signature, Vec<(indirect_ast::Sequent, SequentAnalysis)>) = {
    () => (Signature::new(), vec![]),
    <mut theory: Theory> <decl: Declaration> => {
        let (sig, axioms) = &mut theory;
        match decl {
            Declaration::Sort(sort) => sig.add_sort(sort),
            Declaration::Predicate(pred) => sig.add_predicate(pred),
            Declaration::Function(func) => sig.add_function(func),
            Declaration::Axiom(seq) => { 
                let indirect_seq = indirect_ast::direct_to_indirect(&seq);
                let analysis = analyze(&indirect_seq, &sig);
                axioms.push((indirect_seq, analysis));
            },
        };
        theory
    },
}
